(in-package :gefjon-utils)

(compiler-defun accessor-name (class-name slot-name)
  (symbol-concatenate class-name '- slot-name))

(defmacro err-uninit ()
  '(error "required field uninit"))

(compiler-defun accessor-type-declamation (class-name slot-descriptor)
  `(ftype (function (,class-name) ,(slot-descriptor-type slot-descriptor))
          ,(accessor-name class-name (slot-descriptor-name slot-descriptor))))

(compiler-state
  (cl:defstruct (slot-descriptor (:type list))
    (name (err-uninit) :type symbol)
    (type (err-uninit))))

(compiler-defun slot-descriptors-types (slot-descriptors)
  (mapcar #'slot-descriptor-type slot-descriptors))

(compiler-defun slot-descriptors-names (slot-descriptors)
  (mapcar #'slot-descriptor-name slot-descriptors))

(compiler-defun struct-slot (slot-descriptor)
  `(,(slot-descriptor-name slot-descriptor)
     (err-uninit)
     :type ,(slot-descriptor-type slot-descriptor)))

(compiler-defun struct-slots (slot-descriptors)
  (iter (for slot in slot-descriptors)
        (collecting (struct-slot slot))))

(compiler-defun class-slot (class-name slot-descriptor)
  (destructuring-bind (slot-name slot-type) slot-descriptor
    `(,slot-name
       :initform (err-uninit)
       :initarg ,(make-keyword slot-name)
       :type ,slot-type
       :accessor ,(accessor-name class-name slot-name))))

(compiler-defun class-slots (class-name slot-descriptors)
  (iter (for slot in slot-descriptors)
        (collecting (class-slot class-name slot))))

(compiler-defun slot-initializer-arg (slot-name)
  `(,(make-keyword slot-name) ,slot-name))

(compiler-defun slot-initializers (slot-names)
  (alexandria:mappend #'slot-initializer-arg
                      slot-names))

(compiler-defun constructor-name (class-name)
  (symbol-concatenate 'make- class-name))

(defmacro defclass-boa-constructor (class-name
                                          slot-descriptors
                                          &key (constructor-arglist (slot-descriptors-names slot-descriptors))
                                          (type-arglist (slot-descriptors-types slot-descriptors))
                                          (constructor-name (constructor-name class-name)))
  `(progn
     (declaim (ftype (function ,type-arglist ,class-name)
                     ,constructor-name))
     (defun ,constructor-name ,constructor-arglist
       (make-instance ',class-name
                      ,@(slot-initializers (slot-descriptors-names slot-descriptors))))))

(defmacro defclass (class-name slot-descriptors &key (documentation "generated by STATIC-TYPES:DEFCLASS")
                                                  superclasses)
  
  `(progn
     (cl:defclass ,class-name ,superclasses
       ,(class-slots class-name slot-descriptors)
       (:documentation ,documentation))

     (defclass-boa-constructor ,class-name ,slot-descriptors)))

(compiler-defun defstruct-boa-constructor (struct-name slot-descriptors)
  `(:constructor ,(constructor-name struct-name)
                 ,(slot-descriptors-names slot-descriptors)))

(defmacro declaim-accessor-types (struct-name slot-descriptors)
  (flet ((slot-accessor-declamation (slot)
           (accessor-type-declamation struct-name slot)))
    `(declaim ,@(mapcar #'slot-accessor-declamation slot-descriptors))))

(defmacro defstruct (struct-name slot-descriptors &key (documentation "generated by STATIC-TYPES:DEFSTRUCT")
                                                    repr-type)
  (let ((options (remove nil `(,(defstruct-boa-constructor struct-name slot-descriptors)
                                ,(when repr-type `(:type ,repr-type))
                                ;; passing :NAMED is a no-op (i.e. safe) when not using an explicit :TYPE
                                :named))))
    `(progn
       ,(when repr-type
          ;; if we're passing a :TYPE to CL:DEFSTRUCT, then we have to
          ;; generate our own DEFTYPE form
          `(deftype ,struct-name ()
             '(and ,repr-type (satisfies ,(symbol-concatenate struct-name '-p)))))
       (declaim-accessor-types ,struct-name ,slot-descriptors)

       (cl:defstruct (,struct-name ,@options)
         ,documentation
         ,@(struct-slots slot-descriptors)))))
